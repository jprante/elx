dependencies {
    testImplementation testLibs.junit.jupiter.api
    testImplementation testLibs.junit.jupiter.params
    testImplementation testLibs.hamcrest
    testRuntimeOnly testLibs.junit.jupiter.engine
    testRuntimeOnly testLibs.junit.jupiter.platform.launcher
}

test {
    useJUnitPlatform()
    jvmArgs = [
            // gradle default of 512m is too small for ES bulk indexing
            '-Xms1g',
            '-Xmx2g',
            // for Lucene to access jdk.internal.ref and jdk.internal.misc in Java 11+
            '--add-exports=java.base/jdk.internal.ref=ALL-UNNAMED',
            '--add-exports=java.base/jdk.internal.misc=ALL-UNNAMED',
            '--add-opens=java.base/java.nio=ALL-UNNAMED'
    ]
    systemProperty 'java.util.logging.manager', 'org.apache.logging.log4j.jul.LogManager'
    systemProperty 'io.netty.tryReflectionSetAccessible', 'false'
    systemProperty 'jna.debug_load', 'true'
    systemProperty 'path.home', "${project.getLayout().getBuildDirectory().get().toString()}/"
    failFast = false
    testLogging {
        events 'STARTED', 'PASSED', 'FAILED', 'SKIPPED'
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}
