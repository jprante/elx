dependencies {
    testImplementation libs.junit.jupiter.api
    testImplementation libs.junit.jupiter.params
    testImplementation libs.hamcrest
    testRuntimeOnly libs.junit.jupiter.engine
}

test {
    useJUnitPlatform()
    jvmArgs = [
            // gradle default of 512m is too less for ES bulk indexing
            '-Xmx2g',
            '-Xms2g',
            // for Lucene to access jdk.internal.ref and jdk.internal.misc in Java 11+
            '--add-exports=java.base/jdk.internal.ref=ALL-UNNAMED',
            '--add-exports=java.base/jdk.internal.misc=ALL-UNNAMED',
            '--add-opens=java.base/java.nio=ALL-UNNAMED'
    ]
    systemProperty 'java.util.logging.manager', 'org.apache.logging.log4j.jul.LogManager'
    systemProperty 'jna.debug_load', 'true'
    systemProperty 'path.home', "${project.buildDir}/"
    failFast = true
    testLogging {
        events 'STARTED', 'PASSED', 'FAILED', 'SKIPPED'
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}
